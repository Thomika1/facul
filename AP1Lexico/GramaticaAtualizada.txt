Programa = ':' 'DEC' ListaDeclaracoes ':' 'PROG' ListaComandos;

ListaDeclaracoes = Declaracao (';' Declaracao)*;

Declaracao = VARIAVEL ':' TipoVar;

TipoVar = 'INT' | 'REAL';

ExpressaoAritmetica = TermoAritmetico (('+' | '-') TermoAritmetico)*;

TermoAritmetico = FatorAritmetico (('*' | '/') FatorAritmetico)*;

FatorAritmetico = NUMINT | NUMREAL | VARIAVEL | '(' ExpressaoAritmetica ')';

ExpressaoRelacional = TermoRelacional (OperadorBooleano TermoRelacional)*;

TermoRelacional = ExpressaoAritmetica OP_REL ExpressaoAritmetica | '(' ExpressaoRelacional ')';

OperadorBooleano = 'E' | 'OU';

ListaComandos = Comando (';' Comando)*;

Comando = ComandoAtribuicao | ComandoEntrada | ComandoSaida | ComandoCondicao | ComandoRepeticao | SubAlgoritmo;

ComandoAtribuicao = VARIAVEL ':=' ExpressaoAritmetica;

ComandoEntrada = 'LER' VARIAVEL;

ComandoSaida = 'IMPRIMIR' (VARIAVEL | CADEIA);

ComandoCondicao = 'SE' ExpressaoRelacional 'ENTAO' Comando ('SENAO' Comando)?;

ComandoRepeticao = 'ENQTO' ExpressaoRelacional Comando;

SubAlgoritmo = 'INI' ListaComandos 'FIM';
